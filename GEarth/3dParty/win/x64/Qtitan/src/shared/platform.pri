QT_VERSION = $$[QT_VERSION]
QT_VERSION = $$split(QT_VERSION, ".")
QT_VER_MAJ = $$member(QT_VERSION, 0)
QT_VER_MIN = $$member(QT_VERSION, 1)

#QTN_STATIC_LIB=1
QTN_PRIVATE_HEADERS=1

equals(QTN_PRIVATE_HEADERS, 1) {
  greaterThan(QT_MAJOR_VERSION, 4):CONFIG+=private_headers
}

private_headers {
  DEFINES += QTN_PRIVATE_HEADERS
}

defineReplace(toNativeSeparators) {
    return($$replace(1, /, $$QMAKE_DIR_SEP))
}
defineReplace(fromNativeSeparators) {
    return($$replace(1, \\\\, /))
} 

defineReplace(cleanPath) {
    win32:1 ~= s|\\\\|/|g
    contains(1, ^/.*):pfx = /
    else:pfx =
    segs = $$split(1, /)
    out =
    for(seg, segs) {
        equals(seg, ..):out = $$member(out, 0, -2)
        else:!equals(seg, .):out += $$seg
    }
    return($$join(out, /, $$pfx))
} 

RCC = $$toNativeSeparators($$cleanPath($$[QT_INSTALL_BINS]/rcc))
LRELEASE = $$toNativeSeparators($$cleanPath($$[QT_INSTALL_BINS]/lrelease))
LUPDATE = $$toNativeSeparators($$cleanPath($$[QT_INSTALL_BINS]/lupdate))
LCONVERT = $$toNativeSeparators($$cleanPath($$[QT_INSTALL_BINS]/lconvert))
QMAKE_BINARY = $$toNativeSeparators($$cleanPath($$[QT_INSTALL_BINS]/qmake))

win32 {
    RCC = $${RCC}.exe
    LRELEASE = $${LRELEASE}.exe
    LUPDATE = $${LUPDATE}.exe
    LCONVERT = $${LCONVERT}.exe
    QMAKE_BINARY = $${QMAKE_BINARY}.exe
} 

win32 {
    DEFINES += _WINDOWS
}

win32-msvc2012 {
      QTITAN_LIB_PREFIX=
}

win32-msvc2010 {
      QTITAN_LIB_PREFIX=
}

win32-msvc2008 {
      QTITAN_LIB_PREFIX=
}

win32-msvc2005 {
      QTITAN_LIB_PREFIX=
}

win32-g++ {
      QTITAN_LIB_PREFIX=
}

